#compdef nheko

function _nheko_select_profile() {
    local -a profiles=(default)
    while read -r line; do
        [[ ${line} =~ '\\auth\\device_id' ]] && profiles+=(${line%%\\*})
    done < ${XDG_CONFIG_HOME:-~/.config}/nheko/nheko.conf
    _values "profile" ${profiles}
}

_arguments \
    {'(--help)-h','(-h)--help'}'[Displays help on commandline options.]' \
    --help-all'[Displays help including Qt specific options.]' \
    {'(--version)-v','(-v)--version'}'[Displays version information.]' \
    --debug'[Enables debug output.]' \
    {'(--log-level)-l','(-l)--log-level'}'[Set the global log level, or a comma-separated list of <component>=<level> pairs, or both.]' \
    {'(--log-type)-L','(-L)--log-type'}"[Set the log output type. A comma-separated list is allowed. The default is 'file,stderr'.]" \
    {'(--profile)-p','(-p)--profile'}'[Create or select profile.]':'profile':'_nheko_select_profile'
